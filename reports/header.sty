*****************************************************************************************************************;
*****************************************************************************************************************;
** Program Name: T_01_01_0_Enrollment.sas
** Project: TRISCEND Japan
** Initial Author: Vinay Kumar
** Initial Release Date: 16MAY2023
** Purpose: Enrollment table
** Program Location: ..\Prgs\Tables
** SAS Version: 9.4
*****************************************************************************************************************;
** Revision History (date of release, author, description):
** 
*****************************************************************************************************************;
** Required Macros and Macro Variables (include location):
**
*****************************************************************************************************************;
** Input Datasets (include location):
**
*****************************************************************************************************************;
** Output Generated (include location):
**
*****************************************************************************************************************;
*****************************************************************************************************************;
options mautosource sasautos=("\\awor-pdfilsrv01\tmtt_sas\standards\macros");
%m_incmac; 
dm 'clear log';dm 'clear output';
%put extracted=&extracted;
options symbolgen mlogic mprint;

%let _popfl=;

  ***Delete datasets from previous run;
  proc datasets kill lib=work memtype=data nolist nodetails;
  quit;

  *---------------------------------------------------------------------------------------*;
  * Input Data                                                                            *;
  *---------------------------------------------------------------------------------------*;
  proc sort data=ads.adsl_&extracted
    out=_adsl;
    by usubjid;
    where ENRLFLS="Y";* and &_cohFL="Y";
  run;

  data _adsl;
    set _adsl;

   
	  trtc=substr(trtp,1,1);
	  trtn=trtpN;
	
    if trtn=1 then TRTN1='Y';
	else if trtn=2 then TRTN2='Y';
  run;

  *Macro variable for N= total count;
  %let tot=;
  proc sql noprint;
    select count(unique(usubjid)) into :tot trimmed from _adsl;
  quit;
  %put **tot=&tot;

  *Analysis Dataset;
  data _final;
    set _adsl;
    by usubjid;
	all=1;
  run;

  *---------------------------------------------------------------------------------------*;
  * Perform All Analysis                                                                  *;
  *---------------------------------------------------------------------------------------*;
  %macro bytrt(grp=, col=);
    proc sort data=_final out=_base;
	  by siteid site usubjid;
	  where &grp='Y';
	run;

    data _shell;
	  set _final end=lastobs;
/*	  where &where;*/
	  by siteid site usubjid;
	  retain ptcnt 0;
	  if &grp='Y' then ptcnt+1;
	  if lastobs then call symput('ptcnt', strip(put(ptcnt,4.)));
	  if last.site then output;
	  keep site siteid;
	run;

	proc sort data=_shell;
	  by site siteid;
	run;
  
    %m_sumCat
    (_dsnIn_      = _base,
	 _setWhere_   = &grp="Y",
     _setCol_     = 1,
     _setRow_     = 01,
     _varNum_     = SITEID);

     proc sort data=_set01 out=_set01b;
	   by SITEID;
	 proc sort data=_shell;
	   by SITEID;
	 run;

	 data &grp;
	   merge _set01b (in=in1) _shell (in=in2);
	   by SITEID;
	   den=&ptcnt;
	   if num=. then num=0;
	   length c&col $50;
	   if den ge 10 then c&col=strip(put(num,4.))||"/"||strip(put(den,4.))||" ("||strip(put(100*(num/den),5.1))||"%)";
	   else if den gt 0 then c&col=strip(put(num,4.))||"/"||strip(put(den,4.));
/**/
/*	   if den ge 10 then c&col=strip(put(100*(num/den),5.1))||"%"||" ("||strip(put(num,4.))||"/"||strip(put(den,4.))||")";*/
/*	   else if den gt 0 then c&col=strip(put(num,4.))||"/"||strip(put(den,4.));*/
	   keep site siteid c&col;
	 run;

	proc sort data=&grp;
	  by site siteid;
	run;

    proc datasets lib=work memtype=data nolist nodetails;
      delete _set01 _set01b _shell _base;
    quit;
  %mend;

  *---------------------------------------------------------------------------------------*;
  * Create Output                                                                         *;
  *---------------------------------------------------------------------------------------*;
  
  %bytrt(grp=ENRLFLS, col=2);
  %bytrt(grp=ENRLFLE, col=3);
  %bytrt(grp=ATFL, col=4);


  data _zfinal;
    merge ENRLFLS ENRLFLE atfl;
	by site siteid;
	if site ne "";
	ord1=input(scan(c2,1),best.);
	rename siteid=desc site=c1;
  run;

  proc sort data=_zfinal;
    by descending ord1 desc c1;
  run;

  %m_obschk;

  *Macro variable for N= total count;
  %let c1n=; %let c2n=; %let c3n=; %let c4n=; %let c5n=;
  proc sql noprint;
    select count(unique(usubjid)) into :c1n trimmed from _final where ENRLFLS='Y';
    select count(unique(usubjid)) into :c2n trimmed from _final where ENRLFLE='Y';
    select count(unique(usubjid)) into :c3n trimmed from _final where ATFL='Y';
  
  quit;
  %put c1n=&c1n; %put c2n=&c2n; %put c3n=&c3n;

  %control_file; %* Grabs Control File and creates titles, footers, and output names *;

  %report_start (&_projloc_\Output\tables\&out_sname..rtf);

  proc report data=_zfinal nowd headline split='|' ps=60 ls=240 style=[font_size=14pt] missing 
    style(header column)=[protectspecialchars=off asis=on] style(header)=[borderbottomwidth=2 just=c];
    column ord1 desc c1 c2 c3 c4;
	define ord1 / order order=data noprint;
    define desc / order center style(header)={cellwidth=1.70in just=c} "Site Number";
    define c1 / order center style(header)={cellwidth=3.75in just=c} "Site Name";
    define c2   / center style(header)={cellwidth=1.10in just=c} "Enrolled|(N=&c1n.)";
    define c3   / center style(header)={cellwidth=1.10in just=c} "Attempted|(N=&c2n.)";
    define c4   / center style(header)={cellwidth=1.10in just=c} "Implanted|(N=&c3n.)";
    %rtf_titles;
  run;
  %report_end;

  *-------------------------------------------------------------------------------------*;
  * Output QC                                                                           *;
  *-------------------------------------------------------------------------------------*;

  data tlf.&out_sname.;
    set _zfinal(keep=desc c:);
  run;

